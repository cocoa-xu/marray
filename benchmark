#!/usr/bin/env escript
%% -*- erlang -*-
%%! -pa ./_build/default/lib/marray/ebin -Wall
main([REPEAT_TIMES | ARRAY_LENGTH]) ->
    try
        R = list_to_integer(REPEAT_TIMES),
        lists:foreach(
            fun(X) ->
                N = list_to_integer(X),
                benchmark(N, R),
                io:format("~n")
            end,
            ARRAY_LENGTH
        )
    catch
        A:B:C ->
            io:format("Error: ~p: ~p~n~p~n", [A, B, C]),
            usage()
    end;
main(_) ->
    usage().

usage() ->
    io:format("usage: benchmark REPEAT_TIMES [[ARRAY_LENGTH...]]~n"),
    halt(1).

generate_list(N) when N > 0 ->
    generate_list(N, []).
generate_list(0, Acc) ->
    Acc;
generate_list(N, Acc) ->
    RandomInt = rand:uniform(100 * N),
    generate_list(N - 1, [RandomInt | Acc]).

benchmark(N, R) when N > 0 ->
    io:format("Benchmarking with array length ~p~n", [N]),
    Benchmark = fun(Fun) ->
        {_Pid, Ref} = spawn_monitor(fun() ->
            {Time, _} = timer:tc(Fun, []),
            exit(Time)
        end),
        receive
          {'DOWN', Ref, _, _, Time} -> Time
        end
    end,
    ElapsedTimes = lists:map(
        fun(_I) ->
            List = generate_list(N),
            Marray = marray:from_list(List),
            %% Assert the results match
            true = (marray:to_list(benchmark_marray(marray:from_list(List), c_sort)) =:= benchmark_list(List)),
            [
              Benchmark(fun() -> benchmark_marray(Marray, erlang) end),
              Benchmark(fun() -> benchmark_marray(Marray, c_sort) end),
              Benchmark(fun() -> benchmark_list(List) end)
            ]
        end,
        lists:seq(1, R)
    ),
    [MarrayErlangTimes, MarrayCTimes, ListTimes] = lists:foldr(
        fun([MarrayErlangTime, MarrayCTime, ListTime], [MarrayErlangTimes, MarrayCTimes, ListTimes]) ->
            [[MarrayErlangTime | MarrayErlangTimes], [MarrayCTime | MarrayCTimes],[ListTime | ListTimes]]
        end,
        [[], [], []],
        ElapsedTimes
    ),
    MarrayErlangAvg = lists:sum(MarrayErlangTimes) / length(MarrayErlangTimes),
    MarrayCAvg = lists:sum(MarrayCTimes) / length(MarrayCTimes),
    ListAvg = lists:sum(ListTimes) / length(ListTimes),
    MarrayErlangMax = lists:max(MarrayErlangTimes),
    MarrayCMax = lists:max(MarrayCTimes),
    ListMax = lists:max(ListTimes),
    MarrayErlangMin = lists:min(MarrayErlangTimes),
    MarrayCMin = lists:min(MarrayCTimes),
    ListMin = lists:min(ListTimes),
    MarrayErlangStdDev = math:sqrt(lists:sum(lists:map(fun(X) -> math:pow(X - MarrayErlangAvg, 2) end, MarrayErlangTimes)) / length(MarrayErlangTimes)),
    MarrayCStdDev = math:sqrt(lists:sum(lists:map(fun(X) -> math:pow(X - MarrayCAvg, 2) end, MarrayCTimes)) / length(MarrayCTimes)),
    ListStdDev = math:sqrt(lists:sum(lists:map(fun(X) -> math:pow(X - ListAvg, 2) end, ListTimes)) / length(ListTimes)),
    MarrayErlangStdDev1 = round(MarrayErlangStdDev * 100) / 100,
    MarrayCStdDev1 = round(MarrayCStdDev * 100) / 100,
    ListStdDev1 = round(ListStdDev * 100) / 100,
    io:format("Marray(erlang): avg=~p, max=~p, min=~p, stddev=~p~n", [MarrayErlangAvg, MarrayErlangMax, MarrayErlangMin, MarrayErlangStdDev1]),
    io:format("Marray(c):      avg=~p, max=~p, min=~p, stddev=~p~n", [MarrayCAvg, MarrayCMax, MarrayCMin, MarrayCStdDev1]),
    io:format("List:           avg=~p, max=~p, min=~p, stddev=~p~n", [ListAvg, ListMax, ListMin, ListStdDev1]),
    ok;
benchmark(_, _) ->
    usage().

benchmark_marray(Marray, erlang) ->
    sort(Marray, marray:size(Marray));
benchmark_marray(Marray, c_sort) ->
    marray:sort(Marray).

sort(Marray, 0) ->
    Marray;
sort(Marray, 1) ->
    Marray;
sort(Marray, 2) ->
    Val1 = marray:get(Marray, 0),
    Val2 = marray:get(Marray, 1),
    case Val1 < Val2 of
        true ->
            Marray;
        false ->
            marray:swap(Marray, 0, 1)
    end;
sort(Marray, 3) ->
    Val1 = marray:get(Marray, 0),
    Val2 = marray:get(Marray, 1),
    Val3 = marray:get(Marray, 2),
    case Val1 < Val2 of
        true ->
            case Val2 < Val3 of
                true ->
                    Marray;
                false ->
                    case Val1 < Val3 of
                        true ->
                            marray:swap(Marray, 1, 2);
                        false ->
                            marray:swap(Marray, 0, 2)
                    end
            end;
        false ->
            case Val1 < Val3 of
                true ->
                    marray:swap(Marray, 0, 1);
                false ->
                    case Val2 < Val3 of
                        true ->
                            marray:swap(Marray, 0, 2);
                        false ->
                            marray:swap(Marray, 0, 1),
                            marray:swap(Marray, 1, 2)
                    end
            end
    end;
sort(Marray, N) ->
    quicksort(Marray, 0, N - 1).

quicksort(Marray, L, R) when L < R ->
    PivotIndex = partition(Marray, L, R),
    quicksort(Marray, L, PivotIndex - 1),
    quicksort(Marray, PivotIndex + 1, R);
quicksort(Marray, _L, _R) ->
    Marray.

partition(Marray, L, R) ->
    Pivot = case R - L of
        0 ->
            marray:get(Marray, R);
        1 ->
            marray:get(Marray, R);
        _ ->
            P = L + round((R - L) / 2),
            Val1 = marray:get(Marray, L),
            Val2 = marray:get(Marray, P),
            Val3 = marray:get(Marray, R),
            case Val1 < Val2 of
                true ->
                    case Val2 < Val3 of
                        true ->
                            marray:swap(Marray, P, R),
                            Val2;
                        false ->
                            case Val1 < Val3 of
                                true ->
                                    Val3;
                                false ->
                                    marray:swap(Marray, L, R),
                                    Val1
                            end
                    end;
                false ->
                    case Val1 < Val3 of
                        true ->
                            marray:swap(Marray, L, R),
                            Val1;
                        false ->
                            case Val2 < Val3 of
                                true ->
                                    marray:get(Marray, R);
                                false ->
                                    marray:swap(Marray, P, R),
                                    Val2
                            end
                    end
            end
    end,
    I = partition(R, L, L - 1, Pivot, Marray),
    marray:swap(Marray, I + 1, R),
    I + 1.

partition(R, L, I, Pivot, Marray) when L =< R ->
    Val = marray:get(Marray, L),
    case Val < Pivot of
        true ->
            marray:swap(Marray, I + 1, L),
            partition(R, L + 1, I + 1, Pivot, Marray);
        false ->
            partition(R, L + 1, I, Pivot, Marray)
    end;
partition(_R, _L, I, _Pivot, _Marray) ->
    I.

benchmark_list(List) ->
    lists:sort(List).
